// Main build script for Example
// 
// Usage: gradlew assembleDebug           Build debug APK
//        gradlew assembleRelease         Build release APK
//        gradlew test                    Run local unit test
//        gradlew connectedAndroidTest    Run instrumented unit test

final VERSION_MAJOR = 1 // max two digits
final VERSION_MINOR = 0 // max two digits
final VERSION_PATCH = 0 // max two digits
final VERSION_BUILD = 0 // max three digits

apply plugin: "com.android.application"
apply from: "../utils.gradle"

android {
	compileSdkVersion 27
	buildToolsVersion "27.0.2"

	defaultConfig {
		applicationId "com.example"
		minSdkVersion 16
		targetSdkVersion 27
		versionCode getVersionCode(VERSION_MAJOR, VERSION_MINOR, VERSION_PATCH, VERSION_BUILD)
		versionName getVersionName(VERSION_MAJOR, VERSION_MINOR, VERSION_PATCH)
		testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
	}

	signingConfigs {
		release {
			// passwords and alias are obtained via askForPasswords task
			storeFile file("../${project.property('keystore.file')}")
			storePassword ""
			keyAlias ""
			keyPassword ""
		}
	}

	buildTypes {
		debug {
			buildConfigField "boolean", "LOGS", "true"
			buildConfigField "boolean", "DEV_ENVIRONMENT", "true"

			versionNameSuffix "-debug"
		}

		release {
			buildConfigField "boolean", "LOGS", "false"
			buildConfigField "boolean", "DEV_ENVIRONMENT", "false"

			signingConfig signingConfigs.release
			zipAlignEnabled true
			minifyEnabled false
			shrinkResources false
			proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
		}
	}

	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}

	dataBinding {
		enabled = true
	}

	applicationVariants.all {
		variant ->
			if (variant.buildType.name.equals("release")) {
				renameArtifact("example", variant)
			}
	}
}

dependencies {
	implementation fileTree(dir: "libs", include: ["*.jar"])
	implementation "com.android.support:support-compat:27.0.2"
	implementation "com.android.support:support-core-utils:27.0.2"
	implementation "com.android.support:support-core-ui:27.0.2"
	implementation "com.android.support:support-fragment:27.0.2"
	implementation "com.android.support:appcompat-v7:27.0.2"
	implementation "com.android.support:recyclerview-v7:27.0.2"
	implementation "com.android.support:support-annotations:27.0.2"
	implementation "com.android.support:design:27.0.2"
	implementation "com.google.android.gms:play-services-base:11.6.2"
	testImplementation "junit:junit:4.12"
	testImplementation "org.mockito:mockito-core:2.10.0"
}
